apply plugin: 'com.android.application'

Properties properties = new Properties()
File keysFile = file('keys.properties')

if (keysFile.exists()) {
    properties.load(keysFile.newDataInputStream())
} else if (System.env.CONSUMER_KEY != null) {
    properties.setProperty("consumerKey", System.env.CONSUMER_KEY)
} else {
    throw new GradleException("keys.properties is missing")
}

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.2"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "se.oscarb.wallofpics"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "CONSUMER_KEY", "\"${properties.getProperty("consumerKey")}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'

    compile 'com.facebook.fresco:fresco:0.14.1'

    compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'

    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}
